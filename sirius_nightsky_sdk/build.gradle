buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath("org.openapitools:openapi-generator-gradle-plugin:7.6.0") {
            exclude group: "com.fasterxml.jackson.core"
            exclude group: "com.fasterxml.jackson.dataformat"
            exclude group: "com.fasterxml.jackson.datatype"
            exclude group: "com.fasterxml.jackson"
        }

        classpath "com.fasterxml.jackson:jackson-bom:2.12.3"
        classpath "com.fasterxml.jackson.core:jackson-core:2.12.3"
        classpath "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.3"
        classpath "com.fasterxml.jackson.datatype:jackson-datatype-guava:2.12.3"
        classpath "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.12.3"
        classpath "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.3"
        classpath "com.fasterxml.jackson.core:jackson-databind:2.12.3"
        classpath "com.fasterxml.jackson.core:jackson-annotations:2.12.3"
    }
}

apply plugin: 'org.openapi.generator'
apply plugin: 'java-library'


allprojects {
    ext {
        reactor_version = "3.4.3"
        reactor_netty_version = "1.1.20"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom {
                    licenses {
                        license {
                            name = 'GNU Lesser General Public License, Version 3.0'
                            url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                        }
                    }
                }
            }
        }
    }

    artifactory {
        contextUrl = 'https://bio.informatik.uni-jena.de/repository/'
        publish {
            repository {
                if (version.toString().endsWith('-SNAPSHOT'))
                    repoKey = 'libs-snapshot-local'
                else
                    repoKey = 'libs-release-local'

                username = project.findProperty('de.unijena.bioinf.build.artifactory.username')
                password = project.findProperty('de.unijena.bioinf.build.artifactory.password')
            }
            defaults {
                publications('mavenJava')
            }
        }
    }
}

dependencies {
    api project(":sirius_nightsky_sdk:sirius_nightsky_sdk.openapi")
    api "org.springframework.boot:spring-boot-starter-webflux:$spring_boot_version"
    api "io.projectreactor.netty:reactor-netty-http:$reactor_netty_version"
}

openApiGenerate {
    generatorName.set("java")
    inputSpec.set("$rootDir/sirius_rest_service/openapi-spec.json")
    outputDir.set("$projectDir/sirius_nightsky_sdk.openapi")
    apiPackage.set("de.unijena.bioinf.ms.nightsky.sdk.api")
    invokerPackage.set("de.unijena.bioinf.ms.nightsky.sdk.client")
    modelPackage.set("de.unijena.bioinf.ms.nightsky.sdk.model")
    groupId.set("de.unijena.bioinf.ms")
    version.set(project.version.toString())
    configFile.set("$projectDir/openapi-gen-config.json")
}