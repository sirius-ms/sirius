import org.gradle.crypto.checksum.Checksum
import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
import java.nio.file.*
import java.util.stream.Collectors

plugins {
    id 'edu.sc.seis.launch4j' version '2.5.4'
    id 'org.gradle.crypto.checksum' version '1.4.0'
}

dependencies {
    implementation "org.junit.jupiter:junit-jupiter:$junit_version"
}

version =  siriusFrontendVersion

ext {
    // version for bundeled JRE
    jre_version = '21.0.4'
    zulu_jre_version = '21.36.17'
    jre_major = zulu_jre_version.split("[.]")[0]
    siriusJvmArgs = ['-Xms2G',
                     '-XX:MaxRAMPercentage=75',
//                     '-XX:+UseG1GC',
//                     '-XX:+UseStringDeduplication',
//                     '-XX:+UseZGC',
//                     '-XX:+ZGenerational',
                     '-XX:+ExitOnOutOfMemoryError']
    OS = DefaultNativePlatform.currentOperatingSystem

    if (siriusTargetPlatform.isLinux()) {
        nativeLibs = 'l64'
        osName = 'linux64'
        zosName = 'linux'
        installerNameType = 'deb'
        installerAppendix = "_${version}-1_amd64.$installerNameType"
    } else if (siriusTargetPlatform.isWin()) {
        nativeLibs = 'w64'
        osName = 'win64'
        zosName = 'win'
        installerNameType = 'msi'
        installerAppendix = "-${version.replace('-SNAPSHOT', '')}.$installerNameType"
    } else if (siriusTargetPlatform.isMac()) {
        nativeLibs = 'osx'
        osName = 'osx64'
        zosName = 'macosx'
        installerNameType = 'pkg'
        installerAppendix = "-${version.replace('-SNAPSHOT', '')}.$installerNameType"
    }
}

tasks.register('downloadRuntimeImageFX', Download) {
    String fname = "zulu${zulu_jre_version}-ca-fx-jre${jre_version}-${zosName}_x64${siriusTargetPlatform.isWin() ? ".zip" : ".tar.gz"}"

    src "https://cdn.azul.com/zulu/bin/${fname}"
    dest getBuildDir().toPath().resolve('runtime').resolve(fname).toFile()
    onlyIfModified true
    overwrite false
}

tasks.register('extractRuntimeImageFX', ExtractRuntime) {
    dependsOn downloadRuntimeImageFX
    inputArchive = downloadRuntimeImageFX.dest
}

tasks.register('downloadRuntimeImageHeadless', Download) {
    String fname = "zulu${zulu_jre_version}-ca-jre${jre_version}-${zosName}_x64${siriusTargetPlatform.isWin() ? ".zip" : ".tar.gz"}"

    src "https://cdn.azul.com/zulu/bin/${fname}"
    dest getBuildDir().toPath().resolve('runtime').resolve(fname).toFile()
    onlyIfModified true
    overwrite false
}

tasks.register('extractRuntimeImageHeadless', ExtractRuntime) {
    dependsOn downloadRuntimeImageHeadless
    inputArchive = downloadRuntimeImageHeadless.dest
}

subprojects {
    apply plugin: 'application'
    apply plugin: 'edu.sc.seis.launch4j'

    jar {
        enabled = false
    }
    //remove dependencies that should not be in build
    configurations.each { conf ->
        SiriusPlatform.allBut(siriusTargetPlatform).stream()
                .map { it.inchiClassifier() }.flatMap { it.stream() }
                .forEach { conf.exclude(group: "io.github.dan2097", module: "jna-inchi-$it") }
        if (excludeCbcNative)
            conf.exclude(group: "de.unijena.bioinf.cbc-java", module: "cbc-java-native")

        //remove commercial solver -> given at runtime
        conf.exclude group: "com.gurobi", module: "gurobi-jar"
        conf.exclude group: "cplex", module: "cplex"
    }

    //###################################### Start Default values ##########################################

    //collect auto generated profiles and check them for null values
    tasks.register('collectProfiles') {
        group = 'properties'
        outputs.dir("$buildDir/tmp/profiles/de.unijena.bioinf.ms.defaults")
        doLast {
            outputs.files.singleFile.deleteDir()
            copy {
                from configurations.runtime.resolve().stream().map { f ->
                    zipTree(f).matching {
                        include 'de.unijena.bioinf.ms.defaults/*.auto.config'
                    }
                }.collect(Collectors.toList())

                into "$buildDir/tmp/profiles"
            }
        }


    }
    collectProfiles.dependsOn('compileJava')

    tasks.register('makeProfileTemplate') {
        group = 'properties'
        outputs.file("$buildDir/tmp/profiles_concat/concat.profile")
        doLast {
            outputs.files.singleFile.deleteDir()
            outputs.files.singleFile.withWriter { w ->
                // For each input file path
                tasks.collectProfiles.outputs.files.singleFile.listFiles().each { f ->
                    // Get a reader for the input file
                    f.withReader { r ->
                        // And write data from the input into the output
                        w << r << '\n'
                    }
                }
            }
        }
    }
    makeProfileTemplate.dependsOn('collectProfiles')

    tasks.register('checkDefaultProfile') {
        group = 'properties'
        doLast {
            Properties defaultProfile = new Properties()
            tasks.makeProfileTemplate.outputs.files.singleFile
                    .withReader("UTF8", {
                        defaultProfile.load(it)
                    })

            def errorCount = 0
            logger.lifecycle(System.lineSeparator() + "Checking for NULL values in Profile...")
            defaultProfile.keys().each { e ->
                def v = defaultProfile.getProperty(e)
                if (v == null || ((String) v).isEmpty()) {
                    logger.warn("${e} is NULL!")
                    errorCount++
                }
            }
            if (errorCount > 0) {
                logger.error("FAILED: $errorCount NULL values found!" + System.lineSeparator())
                throw new GradleException("Error: There are NULL values in at least one auto.config files. See Warnings for more detailed information")
            }
            logger.lifecycle("DONE: No NULL values found!" + System.lineSeparator())
        }
    }
    checkDefaultProfile.dependsOn('makeProfileTemplate')

    //###################################### END Default values ##########################################
    launch4j {
        copyright = "Chair of Bioinformatics, Friedrich-Schiller-University Jena."
        companyName = "Friedrich-Schiller-University Jena"

        version = siriusFrontendVersion.toString().replace("-SNAPSHOT", "")
        textVersion = siriusFrontendVersion
        supportUrl = "https://bio.informatik.uni-jena.de/software/sirius/"
        fileDescription = "SIRIUS command-line interface"

        icon = "${rootProject.projectDir}/icons/sirius-icon.ico"
        headerType = "console"
        outfile = "sirius.exe"

        jvmOptions = project.siriusJvmArgs + [
                '-Djava.library.path=.\\'
        ]

        dontWrapJar = true
        classpath = [(libDir == null ? "app" : libDir) + "\\*", "%GUROBI_HOME%\\lib\\gurobi.jar", "%CPLEX_HOME%\\lib\\cplex.jar"]
        libraryDir = libDir == null ? "app" : libDir

        jdkPreference = "preferJdk"
        jreMinVersion = null
        jreMaxVersion = null
        bundledJre64Bit = true
        jreRuntimeBits = "64"
        messagesBundledJreError = "Could not Load bundled JRE."
        bundledJrePath = launch4jJDKPath != null ? launch4jJDKPath : ".\\runtime"
    }

    tasks.register('distImage', AppImage) {
        dependsOn installDist
        if (siriusTargetPlatform.isWin())
            dependsOn createAllExecutables

        jpackagerBin = Paths.get(System.getProperty("java.home")).resolve('bin').resolve(OS.isWindows() ? 'jpackage.exe' : 'jpackage').toFile()
        mainClass = mainClassName
        outputDir = buildDir.toPath().resolve('distributions').toFile()
        winConsole = true


        doLast {
            if (!includeJreFX)
                throw new IllegalArgumentException("OS specific App images need to include java runtime. Please run with `-P build.sirius.starter.jdk.include=true`")
            File appRootTarget
            File launcherTarget
            File runtimeTarget
            File resourceTarget
            if (siriusTargetPlatform.isLinux()) {
                appRootTarget = outputDir.toPath().resolve(imageName).toFile()
                launcherTarget = appRootTarget.toPath().resolve("bin").toFile()
                runtimeTarget = appRootTarget.toPath().resolve("lib/runtime").toFile()
                resourceTarget = appRootTarget
            } else if (siriusTargetPlatform.isWin()) {
                appRootTarget = outputDir.toPath().resolve(imageName).toFile()
                launcherTarget = appRootTarget
                runtimeTarget = appRootTarget.toPath().resolve("runtime").toFile()
                resourceTarget = appRootTarget
            } else if (siriusTargetPlatform.isMac()) {
                appRootTarget = outputDir.toPath().resolve("${imageName}.app/Contents").toFile()
                launcherTarget = appRootTarget.toPath().resolve("MacOS").toFile()
                runtimeTarget = appRootTarget.toPath().resolve("runtime").toFile()
                resourceTarget = appRootTarget.toPath().resolve("Resources").toFile()

            } else {
                throw new RuntimeException("Could not detect OS for packaging!")
            }

            copy {
                from("${rootDir}") {
                    include('COPYING.txt')
                    include('LICENSE.txt')
                }
                into(resourceTarget)
            }

            if (!includeJreFX)
                delete runtimeTarget

            if (siriusTargetPlatform.isWin()) {
                //delete exes because they are write protected an cannot be ovewritten
                delete fileTree(dir: launcherTarget, include: "*.exe")
                copy {
                    from("${buildDir}/launch4j") {
                        include("*.exe")
                    }
                    into(appRootTarget)
                }
                copy {
                    from(zipTree("${rootDir}/dist/ExplorerLicTester.zip"))
                    into { //use closure to move path resolutiop to execution phase
                        getImageDir()
                    }
                }
            } else if (siriusTargetPlatform.isLinux()) {
                delete appRootTarget.toPath().resolve("lib/libapplauncher.so").toFile()
            }

            def templateBinding = [
                    toolName : "SIRIUS",
                    mainClass: mainClassName
            ]

            def f
            def sf
            if (siriusTargetPlatform.isLinux()) {
                f = file("$appRootTarget/bin/sirius")
                sf = file("${rootDir}/scripts/sirius.sh.templ")
                templateBinding.shell = "sh"
                templateBinding.classPath = "\\\"\$JAR_HOME/*\\\":\\\"\$GUROBI_HOME/lib/gurobi.jar\\\":\\\"\$CPLEX_HOME/lib/cplex.jar\\\""
                templateBinding.javaLibPath = ""//includeJreFX ? "\$APP_HOME/lib/runtime/lib" : ""
                templateBinding.gurobiHome = "/lib"
                templateBinding.cplexHome = "/bin/x86-64_linux"
                templateBinding.jvmOpts = "${Arrays.stream(jvmArgs).collect(Collectors.joining(' '))}"
                templateBinding.javaCommand = "\$APP_HOME/lib/runtime/bin/java"
                templateBinding.jarHome = libDir == null ? "\$APP_HOME/lib/app" : libDir
            } else if (siriusTargetPlatform.isMac()) {
                f = file("$appRootTarget/MacOS/sirius")
                sf = file("${rootDir}/scripts/sirius.sh.templ")
                templateBinding.shell = "zsh"
                templateBinding.classPath = "\\\"\$JAR_HOME/*\\\":\\\"\$GUROBI_HOME/lib/gurobi.jar\\\":\\\"\$CPLEX_HOME/lib/cplex.jar\\\""
                templateBinding.javaLibPath = ""//includeJreFX ? "\$APP_HOME/runtime/Contents/Home/lib" : ""
                templateBinding.gurobiHome = "/lib"
                templateBinding.cplexHome = "/bin/x86-64_osx"
                templateBinding.jvmOpts = "${Arrays.stream(jvmArgs).collect(Collectors.joining(' '))}"
                templateBinding.javaCommand = "\$APP_HOME/runtime/Contents/Home/bin/java"
                templateBinding.jarHome = libDir == null ? "\$APP_HOME/app" : libDir
            } else if (siriusTargetPlatform.isWin()) {
                f = file("$appRootTarget/sirius.bat")
                sf = file("${rootDir}/scripts/sirius.bat.templ")
                templateBinding.classPath = "%JAR_HOME%\\*"
                templateBinding.jvmOpts = "${Arrays.stream(jvmArgs).map({ s -> '"' + s + '"' }).collect(Collectors.joining(" "))}"
                templateBinding.jarHome = libDir == null ? "%APP_HOME%\\app" : libDir
                templateBinding.gurobiJar = "%GUROBI_HOME%\\lib\\gurobi.jar"
                templateBinding.cplexJar = "%CPLEX_HOME%\\lib\\cplex.jar"
            } else {
                throw new IllegalArgumentException("Cannot determine operating system for build")
            }
            String script = sf.text
            templateBinding.forEach({ k, v -> script = script.replace("#$k#", v) })
            f.withWriter { it.write(script) }
            f.setExecutable(true, false)

            println("----> DistImage")
            appRootTarget.list().each { println it }
            println()
            println("----> DistImage bin")
            launcherTarget.list().each { println it }
            println()
            println("----> DistImage lib")
            new File(appRootTarget, 'lib').list().each { println it }
            println()
            println("----> DistImage runtime")
            runtimeTarget.list().each { println it }

        }
    }

    tasks.register('distImageTestLocal', Test) {
        enabled false
        group 'verification'
        dependsOn distImage
        useJUnitPlatform()
        testClassesDirs = project(':sirius_dist').sourceSets.test.output
        filter.includeTestsMatching('*Local')

        testLogging {
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }

        doFirst {
            String abs_path = project.projectDir.toString().split('sirius_frontend')[0]
            // automatically adjust for gui_single_os, cli_single_os or service_single_os
            String build_type = project.name.toString()
            String sirius_exec
            String sep


            if (project.OS.isWindows() || project.OS.isLinux() || project.OS.isMacOsX()) {
                sep = System.getProperty("file.separator")
            } else {
                throw new IllegalArgumentException("Could not determine the OS to build on")
            }

            if (project.OS.isWindows()) {
                sirius_exec = abs_path + 'sirius_frontend\\sirius_dist\\' + build_type + '\\build\\distributions\\sirius\\sirius.exe'
            } else if (project.OS.isLinux()) {
                sirius_exec = abs_path + 'sirius_frontend/sirius_dist/' + build_type + '/build/distributions/sirius/bin/sirius'
            } else {
                sirius_exec = abs_path + 'sirius.app' + sep + 'Contents' + sep + 'MacOS' + sep + 'sirius'
            }

            String temp_res_path = abs_path + 'sirius_frontend' + sep + 'sirius_cli' + sep + 'build' + sep + 'test' + sep + 'temp_results'
            String demo_path = abs_path + 'sirius_frontend' + sep + 'sirius_doc' + sep + 'manual' + sep + 'demo-data' + sep

            delete(temp_res_path)

            mkdir temp_res_path
            mkdir temp_res_path + sep + 'mgf_temp_output'
            mkdir temp_res_path + sep + 'mgf_temp_summary'
            mkdir temp_res_path + sep + 'txt_temp_output'
            mkdir temp_res_path + sep + 'txt_temp_summary'
            mkdir temp_res_path + sep + 'ms_temp_output'
            mkdir temp_res_path + sep + 'ms_temp_summary'

            temp_res_path = temp_res_path + sep

            project.exec {
                commandLine([sirius_exec,
                             '--input', demo_path + 'mgf' + sep + 'laudanosine.mgf',
                             '--output', temp_res_path + 'mgf_temp_output',
                             'formula', '-p', 'orbitrap', 'fingerprint', 'structure', 'compound-classes',
                             'write-summaries', '--output', temp_res_path + 'mgf_temp_summary'
                ])
            }

            project.exec {
                commandLine([sirius_exec,
                             '-1', demo_path + 'txt' + sep + 'chelidonine_ms.txt',
                             '-2', demo_path + 'txt' + sep + 'chelidonine_msms1.txt' + ',' + demo_path + 'txt' + sep + 'chelidonine_msms2.txt',
                             '-z', '354.134704589844', '-o', temp_res_path + 'txt_temp_output',
                             'formula', '-p', 'orbitrap',
                             'write-summaries', '--output', temp_res_path + 'txt_temp_summary'
                ])
            }

            project.exec {
                commandLine([sirius_exec,
                             '-o', temp_res_path + 'ms_temp_output',
                             '-i', demo_path + 'ms' + sep + 'Bicuculline.ms',
                             '--ignore-formula', 'formula', '-p', 'orbitrap', '-c', '5',
                             'fingerprint', 'structure', 'compound-classes',
                             'write-summaries', '--output', temp_res_path + 'ms_temp_summary'
                ])
            }
        }
        doLast {
            String abs_path = project.projectDir.toString().split('sirius_frontend')[0]
            String sep = System.getProperty("file.separator")
            String temp_res_path = abs_path + 'sirius_frontend' + sep + 'sirius_cli' + sep + 'build' + sep + 'test' + sep + 'temp_results'
            project.delete(temp_res_path)
        }
    }


    tasks.register('distImageTestCICD', Test) {
        enabled false
        group 'verification'
        useJUnitPlatform()
        testClassesDirs = project(':sirius_dist').sourceSets.test.output
        filter.includeTestsMatching('*Local')

        testLogging {
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }

        doFirst {
            String abs_path = project.projectDir.toString().split('sirius_frontend')[0]
            String sirius_exec = abs_path + 'sirius_frontend/unzipped/sirius/bin/sirius'
            String temp_res_path = abs_path + 'sirius_frontend/sirius_cli/build/test/temp_results'
            String demo_path = abs_path + 'sirius_frontend/sirius_doc/manual/demo-data/'
            String sep = System.getProperty("file.separator")

            delete(temp_res_path)

            mkdir temp_res_path
            mkdir temp_res_path + sep + 'mgf_temp_output'
            mkdir temp_res_path + sep + 'mgf_temp_summary'
            mkdir temp_res_path + sep + 'txt_temp_output'
            mkdir temp_res_path + sep + 'txt_temp_summary'
            mkdir temp_res_path + sep + 'ms_temp_output'
            mkdir temp_res_path + sep + 'ms_temp_summary'

            temp_res_path = temp_res_path + sep

            project.exec {
                commandLine([sirius_exec,
                             '--input', demo_path + 'mgf' + sep + 'laudanosine.mgf',
                             '--output', temp_res_path + 'mgf_temp_output',
                             'formula', '-p', 'orbitrap', 'fingerprint', 'structure', 'compound-classes',
                             'write-summaries', '--output', temp_res_path + 'mgf_temp_summary'
                ])
            }

            project.exec {
                commandLine([sirius_exec,
                             '-1', demo_path + 'txt' + sep + 'chelidonine_ms.txt',
                             '-2', demo_path + 'txt' + sep + 'chelidonine_msms1.txt' + ',' + demo_path + 'txt' + sep + 'chelidonine_msms2.txt',
                             '-z', '354.134704589844', '-o', temp_res_path + 'txt_temp_output',
                             'formula', '-p', 'orbitrap',
                             'write-summaries', '--output', temp_res_path + 'txt_temp_summary'
                ])
            }

            project.exec {
                commandLine([sirius_exec,
                             '-o', temp_res_path + 'ms_temp_output',
                             '-i', demo_path + 'ms' + sep + 'Bicuculline.ms',
                             '--ignore-formula', 'formula', '-p', 'orbitrap', '-c', '5',
                             'fingerprint', 'structure', 'compound-classes',
                             'write-summaries', '--output', temp_res_path + 'ms_temp_summary'
                ])
            }
        }
        doLast {
            String abs_path = project.projectDir.toString().split('sirius_frontend')[0]
            project.delete(abs_path + 'sirius_frontend/sirius_cli/build/test/temp_results')
            project.delete(abs_path + 'sirius_frontend/unzipped')
        }
    }


    tasks.register('signDistImage') {
        group 'distribution'
        dependsOn distImage

        inputs.dir(distImage.outputDir)
        doLast {
            if (siriusTargetPlatform.isMac()) {
                def signID = project.findProperty("de.unijena.bioinf.build.MACOS_APP_IDENTITY_ID")
                if (signID != null && !signID.isBlank()) {
                    def entitlementsFile = getRootProject().getProjectDir().toPath().resolve("dist").resolve("macos.entitlements").toFile()
                    def additionalLibs = [new File(distImage.getImageDir(), "Contents/runtime/Contents/MacOS/libjli.dylib")]
                    def jarNestedLibs = [
                            FileSystems.newFileSystem(distImage.getImageDir().toPath().resolve("Contents").resolve("app").resolve("jna-inchi-darwin-x86-64-1.1.jar"))
                                    .getPath("darwin-x86-64/libjnainchi.dylib")
                    ]

                    getLogger().lifecycle("extract libs nested in the following jars ${Arrays.toString(jarNestedLibs)}")
                    def extractedLibs = getBuildDir().toPath().resolve("tmp").resolve("jarLibs")
                    Files.createDirectories(extractedLibs)
                    for (def inJar : jarNestedLibs) {
                        getLogger().lifecycle("Extracting from jar '${inJar}'")
                        Files.copy(inJar, extractedLibs.resolve(inJar.getFileName().toString()), StandardCopyOption.REPLACE_EXISTING)
                    }

                    getLogger().lifecycle("Signing native libs dir")
                    for (File f : distImage.getImageDir().toPath().resolve("Contents").resolve("native").toFile().listFiles() + additionalLibs + extractedLibs.toFile().listFiles()) {
                        getLogger().lifecycle("Signing native lib '$f'")
                        project.exec {
                            commandLine(["codesign", "-s", "${signID}", "--options", "runtime", "--entitlements", "${entitlementsFile.getAbsolutePath()}", "-f", "-v",
                                         "${f.getAbsolutePath()}"])
                        }
                    }

                    getLogger().lifecycle("Reinsert libs nested in jars")
                    for (def inJar : jarNestedLibs) {
                        getLogger().lifecycle("Reinserting into jar '${inJar}'")
                        Files.copy(extractedLibs.resolve(inJar.getFileName().toString()), inJar, StandardCopyOption.REPLACE_EXISTING)
                        inJar.fileSystem.close()
                    }

                    getLogger().lifecycle("Signing jars dir")
                    for (File f : distImage.getImageDir().toPath().resolve("Contents").resolve("app").toFile().listFiles({ file -> file.getName().endsWith(".jar") } as FileFilter)) {
                        getLogger().lifecycle("Signing jar '$f'")
                        project.exec {
                            commandLine(["codesign", "-s", "${signID}", "--options", "runtime", "--entitlements", "${entitlementsFile.getAbsolutePath()}", "-f", "-v",
                                         "${f.getAbsolutePath()}"])
                        }
                    }

                    println "Signing .app dir"
                    project.exec {
                        commandLine(["codesign", "-s", "${signID}", "--options", "runtime", "--entitlements", "${entitlementsFile.getAbsolutePath()}", "-f", "-v",
                                     "${distImage.getImageDir()}"])
                    }
                } else {
                    println "Could not find ID for signing. APP image will not be signed!"
                }
            } else if (siriusTargetPlatform.isWin()) {
                println "Image signing not needed on Windows"
            } else if (siriusTargetPlatform.isLinux()) {
                println "Image signing not yet implemented for Linux"
            }
        }
    }

    tasks.register('distImageZip') {
        group 'distribution'
        dependsOn distImage
        doFirst {
            def imageDir = distImage.getImageDir()
            def parentPath = imageDir.parentFile.toPath()
            def zipFile = parentPath.resolve("${imageDir.getName()}.zip")
            project.ant.zip(destfile: zipFile, duplicate: 'fail') {
                imageDir.eachFileRecurse { f ->
                    int mode = f.canExecute() ? 755 : 644
                    def relPath = parentPath.relativize(f.toPath()).toString()
                    zipfileset(dir: parentPath, includes: relPath, filemode: mode)
                }
            }
        }
    }
    distImageZip.mustRunAfter(signDistImage)


    tasks.register('distInstaller', AppInstaller) {
        dependsOn distImage
        distImg = distImage
        types = [installerNameType]
        licenseFile = getRootProject().rootDir.toPath().resolve('jpackage').resolve('LICENSE-Full.txt').toFile()

        //linux
        linuxMenuGroup = ['Applications', 'Science']
        debPackageMaintaner = 'sirius@uni-jena.de'

        //win
        winUpdateUUID = '5e660105-397c-4ab7-8d7e-69b4ce2c9564'
        winMenuGroup = 'SIRIUS'
    }
    distInstaller.mustRunAfter(signDistImage)


    tasks.register('signDistInstaller') {
        group 'distribution'
        dependsOn distInstaller
        inputs.dir(distImage.outputDir)
        doLast {
            def source
            def target
            if (siriusTargetPlatform.isMac()) {
                def macInstallerIdentity = project.findProperty('de.unijena.bioinf.build.MACOS_IDENTITY_ID')
                //just to check whether a certificate was given
                if (macInstallerIdentity != null && !macInstallerIdentity.isBlank()) {
                    source = Path.of(distImage.getImageDir().absolutePath.replace('.app', installerAppendix))
                    target = Path.of(distImage.getImageDir().absolutePath.replace('.app', "-SIGNED") + installerAppendix)
                    getLogger().lifecycle("Signing INSTALLER '$source'")
                    project.exec {
                        commandLine(["/usr/bin/productsign", "--timestamp", "--sign", "${macInstallerIdentity}",
                                     "$source", "$target"
                        ])
                    }

                    getLogger().lifecycle("Replacing source INSTALLER with signed installer")
                    if (target != null && source != null && Files.exists(target))
                        Files.move(target, source, StandardCopyOption.REPLACE_EXISTING)
                } else {
                    getLogger().warn("Could not find apple identity for signign installer. Skipping signing step")
                }
            } else if (siriusTargetPlatform.isWin()) {
                def input = distImage.getImageDir().absolutePath + installerAppendix
                def certFile = project.findProperty('de.unijena.bioinf.build.WIN_INSTALLER_CERTIFICATE_FILE')
                getLogger().lifecycle("Cert File location to use: '$certFile'")
                if (certFile != null && !certFile.isBlank() && file(certFile).exists()) {
                    def searchPaths = [Path.of("${System.getenv("SystemDrive")}\\").resolve("Program Files (x86)").resolve("Windows Kits"), Path.of("${System.getenv("SystemDrive")}\\").resolve("Program Files").resolve("Windows Kits")]
                    getLogger().lifecycle("Searching recursively for signtool.exe in ${searchPaths}")
                    def signtoolExe = searchPaths.stream().filter({ p -> Files.exists(p) }).flatMap({ p -> Files.walk(p) })
                            .filter({ p -> p.getFileName().toString().equalsIgnoreCase('signtool.exe') })
                            .findAny().orElseThrow({ new FileNotFoundException("Could not find signtool.exe!") })
                            .toAbsolutePath().toString()

                    getLogger().lifecycle("Testing  signtool.exe found in '$signtoolExe'")
                    project.exec {
                        commandLine(["$signtoolExe", "/h"])
                        standardOutput = System.out
                    }

                    getLogger().lifecycle("Signing MSI installer using signtool.exe '$input'")
                    def c = ["$signtoolExe", "sign", "/debug", "/f", "${certFile}",
                             "/d", "Installer for the SIRIUS MS/MS analysis Software",
                             "/du", "https://bright-giant.com",
                             "/p", "${findProperty('de.unijena.bioinf.build.WIN_INSTALLER_CERTIFICATE_PW')}", "/v", "/fd", "SHA256",
                             "/tr", "\"${findProperty('de.unijena.bioinf.build.WIN_INSTALLER_CERTIFICATE_TIMESTAMP_URL')}\"", "/td", "SHA256", "$input"]
                    println(c)
                    project.exec {
                        commandLine(c)
                        standardOutput = System.out
                    }
                } else {
                    getLogger().warn("Could not find certificate file for signign installer. Skipping signing step")
                }
            } else if (siriusTargetPlatform.isLinux()) {
                println "Installer signing not yet implemented for Linux"
            }
        }
    }

    tasks.register('macNotarize') {
        group 'distribution'
        dependsOn signDistInstaller
        inputs.dir(distImage.outputDir)
        doLast {
            if (siriusTargetPlatform.isMac()) {
                def appleId = project.findProperty('de.unijena.bioinf.build.MACOS_APPLE_ID')
                def appleIdPw = project.findProperty('de.unijena.bioinf.build.MACOS_APPLE_ID_PW')

                if (appleId != null && appleIdPw != null && !appleId.isBlank() && !appleIdPw.isBlank()) {
                    def pkg = Path.of(distImage.getImageDir().absolutePath.replace('.app', installerAppendix))
                    getLogger().lifecycle("Showing available providers")
                    project.exec {
                        commandLine(["xcrun", "altool", "--list-providers",
                                     "-u", "$appleId",
                                     "-p", "$appleIdPw"])
                    }

                    getLogger().lifecycle("Submit for  notarization")
                    String uuid = null
                    new ByteArrayOutputStream().withStream { os ->
                        project.exec {
                            commandLine(["xcrun", "altool", "--notarize-app", "--primary-bundle-id", "app.${distImage.imageName}-$version",
                                         "-u", "$appleId",
                                         "-p", "$appleIdPw",
                                         "--file", "$pkg"])
                            standardOutput = os
                        }
                        uuid = (os.toString().split(System.lineSeparator()) as List).stream().filter({ s -> s.contains("RequestUUID = ") }).findFirst() map({ s -> s.split("RequestUUID = ")[1].strip() }).orElseThrow()
                        println("Submitted Notarization Job with RequestUUID: $uuid")
                    }

                    Map<String, String> states = ["Status": "in progress"]
                    getLogger().lifecycle("Waiting  for  notarization")
                    while ("in progress" == states.get("Status")) {
                        new ByteArrayOutputStream().withStream { os ->
                            def c = ["xcrun", "altool", "--notarization-info", "${uuid}", "-u", "$appleId", "-p", "$appleIdPw"]
                            println(Arrays.toString(c))
                            project.exec {
                                commandLine(c)
                                standardOutput = os
                            }

                            states = (os.toString().split(System.lineSeparator()) as List).stream().map({ s -> s.split("\\s*[:]\\s*") as List })
                                    .collect(Collectors.toMap({ k -> k.get(0).strip() }, { v -> v.size() > 1 ? String.join(':', v.subList(1, v.size())).strip() : "N/A" }))
                            println(states)
                            println("Waiting 30s before retry...")
                        }
                        sleep(30000)
                    }

                    if (states.get("Status Code") == "0" && states.get("Status") == "success") {
                        new ByteArrayOutputStream().withStream { os ->
                            project.exec {
                                commandLine(["xcrun", "stapler", "staple", "${pkg}"])
                                standardOutput = os
                            }.assertNormalExitValue()
                            getLogger().lifecycle(os.toString())
                        }
                    } else {
                        getLogger().lifecycle("Status Code: " + states.get("Status Code"))
                        getLogger().lifecycle("Status: " + states.get("Status"))
                        getLogger().lifecycle("Status Message: " + states.get("Status Message"))
                        throw new GradleException("Error during notarization")
                    }
                } else {
                    getLogger().warn("Could not find apple id for notarization. Skipping notarization step")
                }
            }
        }
    }

    tasks.register('createChecksums', Checksum) {
        group 'distribution'
        mustRunAfter('distImageZip', 'distInstaller', 'macNotarize', 'signDistImage', 'signDistInstaller', 'distZip', 'distTar', 'siriusDistZip', 'siriusDistTar')
        inputFiles.setFrom(fileTree(dir: "$buildDir/distributions", includes: ['*.zip', '*.pkg', '*.deb', '*.msi']))
        outputDirectory.set(file("$buildDir/distributions"))
        checksumAlgorithm.set(Checksum.Algorithm.SHA256)
        appendFileNameToChecksum.set(true)
    }

    tasks.register('distribution') { group = 'distribution' }
    artifactoryPublish.dependsOn 'distribution'
}


//////////////////////////////////////////////////////////////////////////////////
abstract class ExtractRuntime extends DefaultTask {
    @InputFile
    File inputArchive

    ExtractRuntime() {
        group = "distribution"
    }

    @OutputDirectory
    def getOutputDir() {
        return new File(inputArchive.absolutePath.replace((project.siriusTargetPlatform.isWin() ? '.zip' : '.tar.gz'), ''))
    }

    @TaskAction
    def execute() {
        //stupid hack to delete write protected files on nix
        getLogger().lifecycle("Unpacking runtime image $inputArchive...")
        if (project.siriusTargetPlatform.isWin()) {
            getOutputDir().deleteDir()
            project.copy {
                from project.zipTree(inputArchive)
                into getOutputDir().getParentFile()
            }
        } else if (project.siriusTargetPlatform.isLinux()) {
            project.exec { commandLine(['rm', '-rf', getOutputDir().absolutePath]) }
            project.exec { commandLine(['tar', 'zxf', inputArchive.absolutePath, '-C', getOutputDir().getParentFile().absolutePath]) }
//            project.exec { commandLine(['unzip', '-q', inputArchive.absolutePath, '-d', getOutputDir().getParentFile().absolutePath]) }
        } else {
            def tmpPath = getOutputDir().getParentFile().absolutePath + "/mac"
            Files.createDirectories(Paths.get(tmpPath))
            project.exec { commandLine(['rm', '-rf', tmpPath]) }
            Files.createDirectories(Paths.get(tmpPath))
            project.exec { commandLine(['tar', 'zxf', inputArchive.absolutePath, '-C', tmpPath]) }
            project.copy {
                from "${tmpPath}/${getOutputDir().getName()}/zulu-${project.jre_major}.jre/Contents/Home"
                into getOutputDir()
            }
        }
    }
}

abstract class AppImage extends DefaultTask {

    public File inputDir
    public File jpackagerBin
    public File outputDir
    public String imageName
    public String[] jvmArgs
    public String[] args
    public String version
    public String mainClass
    public String mainJarName
    public File runtimeImageDirectory
    public boolean winConsole
    public Map<String, File> launchers = [:]


    /*--mac-package-name name string
    Name of the application as it appears in the Menu Bar.
    This can be different from the application name.
    This name must be less than 16 characters long and be suitable for displaying in the menu bar and the application Info window. Defaults to the application name.*/
    public String macPackageName = null

    AppImage() {
        group = "distribution"
        imageName = project.name
        jvmArgs = []
        version = project.siriusTargetPlatform.isLinux() ? project.siriusFrontendVersion : project.siriusFrontendVersion.replace('-SNAPSHOT', '')
        winConsole = false
        inputDir = project.tasks.getByName('installDist').outputs.files.singleFile.toPath().resolve('lib').toFile()
    }

    @OutputDirectory
    File getImageDir() {
        return outputDir.toPath().resolve(project.siriusTargetPlatform.isMac() ? "${imageName}.app" : imageName).toFile()
    }

    @TaskAction
    def execute() {
        getImageDir().deleteDir()
        String[] p = ['--type', 'app-image']
        p += createParameters()
        project.exec {
            def c = buildCommand(p)
            println c
            commandLine(c)
        }
    }

    def buildCommand(String[] parameters) {
        if (project.OS.isWindows()) {
            getLogger().lifecycle("${jpackagerBin.absolutePath} ${parameters.join(' ')}")
            //because windows command line sucks!
            List<String> c = parameters as List
            c.add(0, "\"${jpackagerBin.absolutePath}\"")
            return c.toArray()
        } else {
            getLogger().lifecycle("${jpackagerBin.absolutePath} ${parameters.join(' ')}")
            return ['bash', '-e', '-c', """
                ${jpackagerBin.absolutePath} ${parameters.join(' ')}
                """]
        }
    }

    String[] createParameters() {
        def paras = [
                '--runtime-image', runtimeImageDirectory.absolutePath,
                '-i', inputDir.absolutePath,
                '--dest', outputDir.toString(),
                '--name', imageName,
                '--app-version', version,
                '--main-class', mainClass,
                '--main-jar', mainJarName
        ]

        if (jvmArgs != null)
            for (String arg : jvmArgs)
                paras += ['--java-options', "\"$arg\"",]

        if (args != null)
            for (String arg : args)
                paras += ['--arguments', "\"$arg\""]

        if (launchers != null && !launchers.isEmpty())
            for (String name : launchers.keySet())
                paras += ["--add-launcher", "\"$name=${launchers.get(name)}\""]


        def iconDir = project.getRootProject().rootDir.toPath().resolve('icons')
        if (project.siriusTargetPlatform.isWin()) {
            paras += ["--icon", "${iconDir.resolve('sirius-icon.ico').toString()}"]
            if (winConsole)
                paras += ['--win-console']
        } else if (project.siriusTargetPlatform.isMac()) {
            paras += ["--icon", "${iconDir.resolve('sirius-icon.icns').toString()}"]
            if (macPackageName != null && !macPackageName.isBlank())
                paras += ['--mac-package-name', "\"$macPackageName\""]
        } else if (project.siriusTargetPlatform.isLinux()) {
            paras += ["--icon", "${iconDir.resolve('sirius-icon.png').toString()}"]
        } else {
            throw new IllegalArgumentException("Could not determine the OS to build on")
        }
        return paras
    }
}

abstract class AppInstaller extends DefaultTask {
    public AppImage distImg
    public String[] types
    public File licenseFile


    public boolean winDirChooser = true
    public boolean winMenu = true
    public String winMenuGroup = null
    public boolean winShortcut
    public boolean winPerUserInstall = false
    public String winUpdateUUID = null


    /*--linux-menu-group <menu-group-name>
          Menu group this application is placed in*/
    public String[] linuxMenuGroup = null
    /* --linux-package-deps
          Required packages or capabilities for the application*/
    public String[] packageDeps = null
    /*--linux-shortcut
          Creates a shortcut for the application*/
    public boolean linuxShortcut = false
    /*--linux-deb-maintainer <email address>
          Maintainer for .deb package*/
    public String debPackageMaintaner = null

    AppInstaller() {
        group = "distribution"
    }

    @InputDirectory
    File getInputDire() {
        return distImg.getImageDir()
    }

    @OutputFile
    File getOutputFile() {
        return distImg.outputDir.toPath().resolve(distImg.imageName + project.installerAppendix).toFile()
    }

    @TaskAction
    def execute() {
        String[] p = ['--type', types.join(',')]
        p += createParameters()
        project.exec {
            def c = distImg.buildCommand(p)
            println c
            commandLine(c)
        }
    }

    @Internal
    String getAppName() {
        return (appName != null && !appName.isBlank()) ? appName : distImg.imageName
    }

    String[] createParameters() {
        String[] paras = []//distImg.createParameters()

        paras += [
                '--verbose',
                '--name', distImg.imageName,
                '--app-image', distImg.getImageDir().absolutePath,
                '--license-file', licenseFile.absolutePath,
                '--dest', distImg.outputDir.absolutePath,
                '--app-version', distImg.version,
//                "--icon", "${project.getRootProject().rootDir.toPath().resolve('icons').resolve('sirius-icon.png').toString()}"
//                '--install-dirr'
        ]


        if (project.siriusTargetPlatform.isWin()) {
            //specify temp directory inside project
            paras += ['--temp', "\"${project.layout.getBuildDirectory().asFile.get().toPath().resolve('tmp').resolve('wix').toAbsolutePath().toString()}\"".toString()]
            //specify resource dir to allow for installer customization.
            paras += ['--resource-dir', "\"${project.getRootProject().rootDir.toPath().resolve('jpackage').resolve('wix-resources').toAbsolutePath().toString()}\"".toString()]
            if (winDirChooser)
                paras += ['--win-dir-chooser'/*, winDirChooser ? '1' : '0'*/]
            if (winMenu)
                paras += ['--win-menu'/*, winMenu ? '1' : '0'*/]
            if (winMenuGroup != null && !winMenuGroup.isBlank())
                paras += ['--win-menu-group', "\"${winMenuGroup}\"".toString()]
            if (winShortcut)
                paras += ['--win-shortcut']
            if (winPerUserInstall)
                paras += ['--win-per-user-install']
            if (winUpdateUUID != null && !winUpdateUUID.isBlank())
                paras += ['--win-upgrade-uuid', "${winUpdateUUID}".toString()]
        } else if (project.siriusTargetPlatform.isMac()) {

        } else if (project.siriusTargetPlatform.isLinux()) {
            if (linuxShortcut)
                paras += ['--linux-shortcut']
            if (debPackageMaintaner != null && !debPackageMaintaner.isBlank())
                paras += ['--linux-deb-maintainer', "\"${debPackageMaintaner}\"".toString()]
            if (linuxMenuGroup != null && linuxMenuGroup.length > 0)
                paras += ['--linux-menu-group', "\"${linuxMenuGroup.join(';')}\"".toString()]
            if (packageDeps != null && packageDeps.length > 0)
                paras += ['--linux-deb-maintainer', "\"${packageDeps.join(',')}\"".toString()]
        } else {
            throw new IllegalArgumentException("Could not determine the OS to build on")
        }
        return paras
    }
}
